/**
 * File:	include/controller/dialogs.ycp
 * Package:	Configuration of controller
 * Summary:	Dialogs definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */
{
textdomain "s390";

import "Label";
import "Mode";
import "Popup";
import "Progress";
import "Report";
import "Sequencer";
import "Wizard";
import "ZFCPController";

include "s390/zfcp/helps.ycp";


    /**
     * List ZFCP devices that are currently being selected
     * @return list<integer> list of IDs of selected ZFCP devices
     */
    list<integer> ListSelectedZFCP()
    {
	list<integer> selected = (list<integer>) UI::QueryWidget(`id(`table), `SelectedItems);
	y2milestone("selected %1", selected);
	return selected;
    }


/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol ReadDialog()
{
    Wizard::RestoreHelp(ZFCP_HELPS["read"]:"");
    boolean ret = ZFCPController::Read();
    return ret ? `next : `abort;
}


/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol WriteDialog()
{
    Wizard::RestoreHelp(ZFCP_HELPS["write"]:"");
    boolean ret = ZFCPController::Write();
    return ret ? `next : `abort;
}


/**
 * Get the list of items for the table of ZFCP devices
 * @param min_chan integer minimal channel number
 * @param max_chan integer maximal channel number
 * @return a list of terms for the table
 */
list<term> GetZFCPDiskItems()
{
    map<integer, map<string, any> > devices = ZFCPController::GetFilteredDevices();

    list<term> items = [];

    if (Mode::config())
    {
	items = maplist(integer k, map<string, any> d, devices, {
		string channel = d["detail", "controller_id"]:"";
		string wwpn = d["detail", "wwpn"]:"";
		string lun = d["detail", "fcp_lun"]:"";
		return `item(`id(k), channel, wwpn, lun);
	    });
    }
    else
    {
	items = maplist(integer k, map<string, any> d, devices, {
		string channel = d["detail", "controller_id"]:"";
		string wwpn = d["detail", "wwpn"]:"";
		string lun = d["detail", "fcp_lun"]:"";
		string dev_name = d["dev_name"]:"";
		return `item(`id(k), channel, wwpn, lun, dev_name);
	    });
    }

    return items;
}


/**
 * Show the ZFCP-Dialog
 */
void DisplayZFCPDialog()
{
    /* Minimal text for the help */
    string help = ZFCP_HELPS["disk_selection"]:"";

    /* Dialog caption */
    string caption = _("Configured ZFCP Devices");

    term header = `Empty();

    if (Mode::config())
    {
	header = `header(
	    // table header
	    `Right(_("Channel ID")),
	    // table header
	    `Right(_("WWPN")),
	    // table header
	    `Right(_("LUN"))
	    );
    }
    else
    {
	header = `header(
	    // table header
	    `Right(_("Channel ID")),
	    // table header
	    `Right(_("WWPN")),
	    // table header
	    `Right(_("LUN")),
	    // table header
	    _("Device")
	    );
    }

    /* Dialog content */
    term content = `VBox(
	`HBox(
	    // text entry
	    `InputField(`id(`min_chan), `opt(`hstretch), _("Mi&nimum Channel ID"), ZFCPController::filter_min),
	    // text entry
	    `InputField(`id(`max_chan), `opt(`hstretch), _("Ma&ximum Channel ID"), ZFCPController::filter_max),
	    `VBox (
		`Label (""),
		// push button
		`PushButton (`id (`filter), _("&Filter"))
	    )
	),
	`Table(`id(`table), `opt(`multiSelection), header, []),
	`HBox(
	    `PushButton(`id(`add), Label::AddButton ()),
	    `PushButton(`id(`delete), Label::DeleteButton ()),
	    `HStretch ()
	)
    );

    Wizard::SetContentsButtons(caption, content, help, Label::BackButton(), Label::NextButton());
    Wizard::HideBackButton();
    Wizard::SetAbortButton(`abort, Label::CancelButton() );

    UI::ChangeWidget(`id(`min_chan), `ValidChars, "0123456789abcdefABCDEF.");
    UI::ChangeWidget(`id(`max_chan), `ValidChars, "0123456789abcdefABCDEF.");
}


/**
 * Restart the ZFCP-Dialog
 */
void ReloadZFCPDialog()
{
    list<term> items = GetZFCPDiskItems();

    list<integer> selected = (list<integer>) UI::QueryWidget(`id(`table), `SelectedItems);
    UI::ChangeWidget(`id(`table), `Items, items);
    UI::ChangeWidget(`id(`table), `SelectedItems, selected);
    UI::SetFocus(`table);
}


/**
 * Show the ZFCP-Dialog
 * @return symbol From the dialog
 */
symbol ZFCPDialog()
{
    DisplayZFCPDialog ();
    ReloadZFCPDialog ();

    symbol ret = nil;
    while (ret == nil)
    {
	ret = (symbol) UI::UserInput();

	if (ret == `filter)
	{
	    string filter_min = (string) UI::QueryWidget(`min_chan, `Value);
	    string filter_max = (string) UI::QueryWidget(`max_chan, `Value);

	    if (!ZFCPController::IsValidChannel(filter_min) || !ZFCPController::IsValidChannel(filter_max))
	    {
		// error popup
		Popup::Error(_("Invalid filter channel IDs."));
		ret = nil;
		continue;
	    }

	    ZFCPController::filter_min = ZFCPController::FormatChannel(filter_min);
	    ZFCPController::filter_max = ZFCPController::FormatChannel(filter_max);

	    ReloadZFCPDialog();
	    ret = nil;
	    continue;
	}
	else if (ret == `table)
	{
	    ret = nil;
	    continue;
	}
	else if (ret == `abort || ret == `cancel)
	{
	    // yes-no popup
	    if (! Popup::YesNo (_("Really leave the ZFCP device configuration without saving?
All changes will be lost.")))
	    {
		ret = nil;
	    }
	    continue;
	}
    }

    return ret;
}


/**
 * Add ZFCP-Dialog
 * @return symbol the dialog
 */
symbol AddZFCPDiskDialog()
{
    /* Minimal text for the help */
    string help = ZFCP_HELPS["disk_add"]:"";

    // dialog caption
    string caption = _("Add New ZFCP Device");

    list<string> channels = [];
    list<string> wwpns = [];
    list<string> luns = [];

    if (Mode::config ())
    {
	channels = maplist(integer index, map<string, any> d, ZFCPController::devices, {
	    return d["detail", "controller_id"]:"";
	});
	channels = toset(channels);

	wwpns = maplist(integer index, map<string, any> d, ZFCPController::devices, {
	    return d["detail", "wwpn"]:"";
	});
	wwpns = toset(wwpns);

	luns = maplist(integer index, map<string, any> d, ZFCPController::devices, {
	    return d["detail", "fcp_lun"]:"";
	});
	luns = toset(luns);
    }
    else
    {
	channels = maplist (map<string, any> c, ZFCPController::GetControllers(), {
		return c["sysfs_bus_id"]:"";
	});

	wwpns = [ ZFCPController::previous_settings["wwpn"]:"" ];

	string lun = ZFCPController::previous_settings["fcp_lun"]:"";
	if (!isempty(lun))
	    lun = ZFCPController::GetNextLUN(lun);

	luns = [ lun ];
    }

    term content = `HBox (`HStretch (), `VBox(
	`VStretch (),
	// combo box
	`ComboBox(`id(`channel), `opt (`editable, `hstretch), _("&Channel ID"), channels),
	`VSpacing (2),
	`HBox(
	    // combo box
	    `ComboBox(`id(`wwpn), `opt(`hstretch, `editable), _("&WWPN"), wwpns),
	    // push button
	    Mode::config() ? `Empty() : `PushButton(`id(`get_wwpn), _("Get WWPNs"))
            ),
	`VSpacing (2),
	`HBox(
	    // combobox
	    `ComboBox(`id(`lun), `opt(`hstretch, `editable), _("&LUN"), luns),
	    // push button
	    Mode::config() ? `Empty() : `PushButton(`id(`get_lun), _("Get LUNs"))
            ),
	`VStretch ()
    ), `HStretch ());

    /* Apply the settings */
    Wizard::SetContents(caption, content, help, true, true);
    Wizard::RestoreBackButton ();
    Wizard::RestoreAbortButton ();

    UI::ChangeWidget(`id(`channel), `ValidChars, "0123456789abcdefABCDEF.");
    UI::ChangeWidget(`id(`wwpn), `ValidChars, "0123456789abcdefABCDEFx");
    UI::ChangeWidget(`id(`lun), `ValidChars, "0123456789abcdefABCDEFx");

    UI::SetFocus(`id(`channel));

    symbol ret = nil;
    while (ret == nil)
    {
	ret = (symbol) UI::UserInput();

	if (ret == `get_wwpn)
	{
	    string channel = (string) UI::QueryWidget(`channel, `Value);

	    if (!ZFCPController::IsValidChannel(channel))
	    {
		// error popup
		Popup::Error(_("Not a valid channel ID."));
		UI::SetFocus(`channel);
		ret = nil;
		continue;
	    }

	    channel = ZFCPController::FormatChannel(channel);

	    wwpns = ZFCPController::GetWWPNs(channel);
	    UI::ChangeWidget(`wwpn, `Items, wwpns);
	    ret = nil;
	}
	else if (ret == `get_lun)
	{
	    string channel = (string) UI::QueryWidget(`channel, `Value);
	    string wwpn = (string) UI::QueryWidget(`wwpn, `Value);

	    if (!ZFCPController::IsValidChannel(channel))
	    {
		// error popup
		Popup::Error(_("Not a valid channel ID."));
		UI::SetFocus(`channel);
		ret = nil;
		continue;
	    }

	    if (!ZFCPController::IsValidWWPN(wwpn))
	    {
		// error popup
		Report::Error(_("The entered WWPN is invalid."));
		UI::SetFocus(`wwpn);
		ret = nil;
		continue;
	    }

	    channel = ZFCPController::FormatChannel(channel);
	    wwpn = ZFCPController::FormatWWPN(wwpn);

	    luns = ZFCPController::GetLUNs(channel, wwpn);
	    UI::ChangeWidget(`lun, `Items, luns);
	    ret = nil;
	}
       else if (ret == `abort || ret == `cancel)
	{
	    // yes-no popup
	    if (! Popup::YesNo (_("Really leave the ZFCP device configuration without saving?
All changes will be lost.")))
	    {
		ret = nil;
	    }
	}
	else if (ret == `next)
	{
	    string channel = (string) UI::QueryWidget(`id(`channel), `Value);
	    string wwpn = (string) UI::QueryWidget(`id(`wwpn), `Value);
	    string lun = (string) UI::QueryWidget(`id(`lun), `Value);

	    if (!ZFCPController::IsValidChannel(channel))
	    {
		// error popup
		Popup::Error(_("Not a valid channel ID."));
		UI::SetFocus(`channel);
		ret = nil;
		continue;
	    }

	    if (!ZFCPController::IsValidWWPN(wwpn))
	    {
		// error popup
		Report::Error(_("The entered WWPN is invalid."));
		UI::SetFocus(`wwpn);
		ret = nil;
		continue;
	    }

	    if (!ZFCPController::IsValidLUN(lun))
	    {
		// error popup
		Report::Error(_("The entered LUN is invalid."));
		UI::SetFocus(`lun);
		ret = nil;
		continue;
	    }

	    channel = ZFCPController::FormatChannel(channel);
	    wwpn = ZFCPController::FormatWWPN(wwpn);
	    lun = ZFCPController::FormatLUN(lun);

	    if (ZFCPController::GetDeviceIndex(channel, wwpn, lun) != nil)
	    {
		// error popup
		Popup::Error(_("Device already exists."));
		ret = nil;
		continue;
	    }
	}
    }

    if (ret == `next)
    {
	string channel = (string) UI::QueryWidget(`id(`channel), `Value);
	string wwpn = (string) UI::QueryWidget(`id(`wwpn), `Value);
	string lun = (string) UI::QueryWidget(`id(`lun), `Value);

	channel = ZFCPController::FormatChannel(channel);
	wwpn = ZFCPController::FormatWWPN(wwpn);
	lun = ZFCPController::FormatLUN(lun);

	ZFCPController::previous_settings["channel"] = channel;
	ZFCPController::previous_settings["wwpn"] = wwpn;
	ZFCPController::previous_settings["fcp_lun"] = lun;

	if (Mode::config())
	{
	    map<string,any> m = $[
		"controller_id" : channel,
		"wwpn" : wwpn,
		"fcp_lun" : lun,
	    ];

	    m = $["detail" : m ];

	    ZFCPController::AddDevice(m);
	}
	else
	{
	    ZFCPController::ActivateDisk (channel, wwpn, lun);

	    ZFCPController::ProbeDisks();
	}
    }

    return ret;
}


/**
 * Run the dialog for deleting ZFCPs
 * @return symbol from DeleteZFCPDiskDialog
 */
symbol DeleteZFCPDiskDialog()
{
    list<integer> selected = ListSelectedZFCP();
    if (isempty(selected))
    {
	// error popup message
	Popup::Message (_("No disk selected."));
    }
    else
    {
	if (Mode::config())
	{
	    foreach(integer index, selected, { ZFCPController::RemoveDevice(index); });
	}
	else
	{
	    foreach(integer index, selected, {
		    map d = ZFCPController::devices[index]:$[];
		    string channel = d["detail", "controller_id"]:"";
		    string wwpn = d["detail", "wwpn"]:"";
		    string lun = d["detail", "fcp_lun"]:"";
		    ZFCPController::DeactivateDisk(channel, wwpn, lun);
		});

	    ZFCPController::ProbeDisks();
	}
    }

    return `next;
}

}
