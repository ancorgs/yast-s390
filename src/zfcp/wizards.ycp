/**
 * File:	include/controller/wizards.ycp
 * Package:	Configuration of controller
 * Summary:	Wizards definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */
{
textdomain "s390";

import "Sequencer";
import "Wizard";

include "s390/zfcp/dialogs.ycp";


/**
 * Main dialog
 * @return symbol dialog
 */
symbol MainZFCPSequence()
{
    map aliases = $[
	"main"		: ``(ZFCPDialog()),
	"add"		: ``(AddZFCPDiskDialog()),
	"delete"	: ``(DeleteZFCPDiskDialog())
    ];

    map sequence = $[
	"ws_start"	: "main",
	"main" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add", 
	    `delete	: "delete"
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"delete" : $[
	    `abort	: `abort,
	    `next	: "main"
	]
    ];

    return Sequencer::Run(aliases, sequence);
}


/**
 * Whole configuration of controller
 * @return sequence result
 */
any ZFCPSequence()
{
    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainZFCPSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("zfcp");

    any ret = Sequencer::Run(aliases, sequence);

    Wizard::CloseDialog();

    return ret;
}


/**
 * Whole configuration of controller but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
any ZFCPAutoSequence()
{
    /* Initialization dialog caption */
    string caption = _("Controller Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("zfcp");
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    any ret = MainZFCPSequence();

    Wizard::CloseDialog();

    return ret;
}

}
