/**
 * File:	clients/xpram.ycp
 * Package:	Configuration of xpram
 * Summary:	Main file
 * Authors:	Ihno Krumreich <Ihno@suse.de>
 *
 * $Id$
 *
 * Main file for xpram configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of xpram</h3>
 */

textdomain "xpram";

/* The main () */
y2milestone ("--------- Xpram module started ---------");

import "CommandLine";
import "Xpram";

include "s390/xpram/ui.ycp";

// --------------------------------------------------------------------------
// --------------------------------- cmd-line handlers

/**
 * Command line handler for changing basic configuration
 * @param options  a list of parameters passed as args
 * (currently only "mountpoint" key is expected)
 * @return boolean true on success
 */
define boolean XpramChangeConfiguration (map options ) {

    string mountpoint = options["mountpoint"]:"";
    if (mountpoint != "")
    {
	Xpram::mountpoint	= mountpoint;
	Xpram::modified	= true;
	return true;
    }
    return false;
}

/**
 * Command line handler for enabling XpRAM
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
define boolean XpramEnableHandler (map options) {

    boolean ret = XpramChangeConfiguration (options);
    if (!Xpram::start)
    {
	Xpram::start	= true;
	ret		= true;
	Xpram::modified	= true;
    }
    return ret;
}

/**
 * Command line handler for disabling XpRAM
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
define boolean XpramDisableHandler (map options) {

    if (Xpram::start)
    {
	Xpram::start	= false;
	Xpram::modified	= true;
	return true;
    }
    return false;
}

map cmdline_description = $[
    "id"		: "xpram",
    /* Command line help text for the Xxpram module */
    "help"		: _("Configuration of XPRAM"),
    "guihandler"        : XpRAMSequence,
    "initialize"        : Xpram::Read,
    "finish"            : Xpram::Write,
    "actions"           : $[
	"enable" :$[
	    "handler"	: XpramEnableHandler,
	    // command line help text for 'enable' action
	    "help"	: _("Enable XPRAM")
	],
	"disable" :$[
	    "handler"	: XpramDisableHandler,
	    // command line help text for 'disable' action
	    "help"	: _("Disable XPRAM")
	],
	"configure"	: $[
	    "handler"	: XpramChangeConfiguration,
	    // command line help text for 'configure' action
	    "help"	: _("Change the XPRAM configuration")
	],
    ],
    "options"		: $[
	"mountpoint"	:$[
	    // command line help text for the 'mountpoint' option
	    "help"	: _("Mount point"),
	    "type"	: "string"
	],
    ],
    "mappings"		: $[
	"enable"	: [ "mountpoint" ],
	"disable"	: [],
	"configure"	: [ "mountpoint" ],
    ]
];


/* main ui function */
any ret = CommandLine::Run (cmdline_description);

y2debug("ret=%1", ret);

/* Finish */
y2milestone ("--------- Xpram module finished ---------");

return ret;

/* EOF */
}
