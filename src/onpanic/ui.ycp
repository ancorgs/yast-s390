/**
 * File:	include/s390/onpanic/ui.ycp
 * Package:	Configuration of OnPanic
 * Summary:	Dialogs definitions
 * Authors:	Tim Hardeck <thardeck@suse.de>
 *
 */

{

textdomain "s390";

import "OnPanic";
import "Label";
import "Message";
import "Package";
import "Popup";
import "Wizard";


/**
 * Should dumpconf be started?
 */
define boolean start	= false;

/**Update the screen according to user input
 * @param return symbol
 * @return void
 */
define void UpdateScreen (symbol ret) {
	if (ret == `yes || ret == `no)
	{
		start = (ret == `yes);
	}

	if (ret == `vmcmd)
	{
		string vmcmds			= (string) UI::QueryWidget (`id (`vmcmd), `Value);
		list<string> vmcmd_list = splitstring (vmcmds, "\n");

		// only five lines are allowed, remove every additional line
		if (size(vmcmd_list) > OnPanic::VMCMD_MAX_ROWS)
		{
			vmcmd_list = sublist(vmcmd_list, 0, OnPanic::VMCMD_MAX_ROWS);
			vmcmds = mergestring(vmcmd_list, "\n");
			UI::ChangeWidget (`id(`vmcmd), `Value, vmcmds);
			Popup::Notify (sformat(_("Only %1 lines are allowed for VMCMD!"), OnPanic::VMCMD_MAX_ROWS));
		}

		// allow max chars + newlines
		integer vmcmd_max_chars = OnPanic::VMCMD_MAX_CHARS + size(vmcmd_list) - 1;
		UI::ChangeWidget( `vmcmd, `InputMaxLength, vmcmd_max_chars);
	}

	if (start)
	{
		UI::ChangeWidget (`id (`rd), `CurrentButton, `yes);
		UI::ChangeWidget (`id (`onpanic), `Enabled, true);
		UI::ChangeWidget (`id (`delayminutes), `Enabled, true);

		// dis/enable widgets according to `onpanic selection
		string on_panic = (string) UI::QueryWidget (`id (`onpanic), `Value);
		UI::ChangeWidget (`id (`dumpdevice), `Enabled,
			(on_panic == "dump" || on_panic == "dump_reipl")
		);
		UI::ChangeWidget (`id (`vmcmd), `Enabled,
			(on_panic == "vmcmd")
		);
	}
	else
	{
		// disable all widgets except of "enable dumpconf"
		UI::ChangeWidget (`id(`rd), `CurrentButton, `no);
		foreach (symbol widget, [`onpanic, `dumpdevice, `vmcmd, `delayminutes],{
				UI::ChangeWidget (`id (widget), `Enabled, false);
		});
	}
}

/**
 * Dialog for seting up OnPanic
 */
define symbol OnPanicDialog () {
	// On Panic Actions
	list<string> actions = [ "stop", "dump", "reipl", "dump_reipl", "vmcmd" ];
	// Mkdump list of dump devices
	list<string> dump_devices = OnPanic::dump_devices;

	// For translators: Caption of the dialog
	string caption = _("On Panic Configuration");
	string help = _("<p><b>Configure the actions to be taken if a kernel panic occurs</b></p>")
+ _("<p>The <b>Dumpconf</b> daemon needs to be enabled to influence the behavior during kernel panics.</p>")
+ _("<p>The following <b>Panic Actions</b> are possible:<br>")
+ _("<b>stop</b> Stop Linux (default).<br>")
+ _("<b>dump</b> Dump Linux and stop system.<br>")
+ _("<b>reipl</b> Reboot Linux.<br>")
+ _("<b>dump_reipl</b> Dump Linux and reboot system. It is only available on LPAR with z9(r) machines and later, and on z/VMversion 5.3 and later.<br>")
+ _("<b>vmcmd</b> Execute specified CP commands and stop system.</p>")
+ _("<p>The time defined in <b>Delay Minutes</b> defers activating the specified panic action for a newly started system to prevent loops. If the system crashes before the time has elapsed the default action (stop) is performed.</p>")
+ _("<p>The device for dumping the memory can be set with <b>Dump Device</b>. If no device is shown you have to create one with the <b>YaST Dump Devices</b> dialog.</p>")
+ sformat(_("<p>With <b>VMCMD</b> CP commands can be specified which are executed before the Linux system is stopped. Only %1 lines and a total of %2 chars are allowed.</p>"), OnPanic::VMCMD_MAX_ROWS, OnPanic::VMCMD_MAX_CHARS);

	term content = `HBox (`HSpacing (3), `VBox (
	`VSpacing (2),
		`VSquash(`HBox(
		`RadioButtonGroup(`id(`rd),
		`HSquash(`VBox(
				`Left (`RadioButton(`id(`no), `opt (`notify),
			// radio button label
			_("Do No&t Start Dumpconf"), !start)),
				`Left(`RadioButton(`id(`yes), `opt (`notify),
			// radio button label
			_("&Start Dumpconf"), start))))),
		`HSpacing (5),
		`VCenter(`HBox(
			`ComboBox (`id(`onpanic), `opt(`notify, `hstretch),
				// combobox label
				_("&Panic Action"), actions),
		`IntField(`id(`delayminutes), _("Delay &Minutes"), 0, 300, 5 )
		)))),
	`VSpacing (),
		`ComboBox (`id(`dumpdevice), `opt(`hstretch),
			// combobox label
			_("&Dump Device"), dump_devices),
	`VSpacing (),
	`MultiLineEdit( `id( `vmcmd ), `opt(`notify), _("&VMCMD"), "" ),
	`VStretch ()
	), `HSpacing(3));


	Wizard::SetContentsButtons (caption, content, help, Label::BackButton (),  Label::OKButton());
	Wizard::HideBackButton();
	Wizard::SetAbortButton(`cancel, Label::CancelButton());

	// set configuration settings if available
	start		= OnPanic::start;
	// initialize fields
	UI::ChangeWidget (`id(`onpanic), `Value, OnPanic::on_panic);
	UI::ChangeWidget (`id(`delayminutes), `Value, OnPanic::delay_min);
	UI::ChangeWidget (`id(`vmcmd), `Value, OnPanic::vmcmds);
	UI::ChangeWidget (`id(`dumpdevice), `Value, OnPanic::dump_line);
	// limit the vmcmd size
	UI::ChangeWidget( `vmcmd, `InputMaxLength, OnPanic::VMCMD_MAX_CHARS);

	symbol ret = nil;
	UpdateScreen(ret);

	do
	{
		ret	= (symbol) UI::UserInput ();
		UpdateScreen(ret);

		// check for changes on final user actions
		if (contains ([`back, `abort, `cancel, `next, `ok, `finish], ret))
		{
			OnPanic::modified = (OnPanic::start != start
								|| OnPanic::on_panic	!= (string) UI::QueryWidget (`id (`onpanic), `Value)
								|| OnPanic::delay_min	!= (integer) UI::QueryWidget (`id (`delayminutes), `Value)
								|| OnPanic::vmcmds		!= (string) UI::QueryWidget (`id (`vmcmd), `Value)
								|| OnPanic::dump_line	!= (string) UI::QueryWidget (`id (`dumpdevice), `Value));

			// if settings were changed don't exit without asking
			if (contains ([`back, `abort, `cancel], ret) && OnPanic::modified
				&& !Popup::ReallyAbort (true))
			{
				ret = `again;
			}

			// check misconfigurations
			if (contains ([`next, `ok, `finish], ret) && start)
			{
				// don't allow dumps if no device is available
				if (regexpmatch((string) UI::QueryWidget (`id (`onpanic), `Value), "^dump")
					&& (string) UI::QueryWidget (`id (`dumpdevice), `Value) == "")
				{
					Popup::Notify (_("It is not possible to enable the dump process without a dump device."));
					ret = `again;
				}

				// don't allow vmcmd without at least one command
				if ((string) UI::QueryWidget (`id (`onpanic), `Value) == "vmcmd"
					&& !regexpmatch((string) UI::QueryWidget (`id (`vmcmd), `Value), "[A-Za-z]{2,}"))
				{
					Popup::Notify (_("It is not possible to use vmcmd  without defining at least one command."));
					ret = `again;
				}

			}
		}
	} while (!contains ([`back, `abort, `cancel, `next, `ok], ret));

	// commit changes
	if (OnPanic::modified && (ret == `next || ret == `ok || ret == `finish))
	{
		OnPanic::start		= start;
		OnPanic::on_panic	= (string) UI::QueryWidget (`id (`onpanic), `Value);
		OnPanic::delay_min	= (integer) UI::QueryWidget (`id (`delayminutes), `Value);
		OnPanic::vmcmds		= (string) UI::QueryWidget (`id (`vmcmd), `Value);
		OnPanic::dump_line	= (string) UI::QueryWidget (`id (`dumpdevice), `Value);
	}

	return ret;
}

/**
 * The whole sequence
 */
define symbol OnPanicSequence () {
	Wizard::CreateDialog ();
	Wizard::SetDesktopIcon("onpanic");

	OnPanic::Read ();

	symbol ret = OnPanicDialog ();
	if (ret == `next || ret == `finish || ret == `ok)
	{
		OnPanic::Write ();
	}

	UI::CloseDialog ();
	return ret;
}

/* EOF */
}
